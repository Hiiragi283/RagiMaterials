buildscript {
    ext.kotlin_version = "1.3.0"
    repositories {
        maven {
            url = "https://maven.minecraftforge.net"
        }
        mavenCentral()
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: "net.minecraftforge.gradle.forge"

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name

sourceCompatibility = targetCompatibility = "1.8"

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"
}

repositories {
    maven {
        url = "https://repo.spongepowered.org/maven"
    }
    mavenCentral()
}

dependencies {
    compile fileTree(dir: "libs", include: "*.jar")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

sourceSets {
    main {
        ext.refMap = "mixins." + archives_base_name + ".refmap.json"
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
    if (project.use_access_transformer.toBoolean()) {
        rename "(.+_at.cfg)", 'META-INF/$1' // Access Transformers
    }
}

jar {
    manifest {
        def attribute_map = [:]
        if (project.use_access_transformer.toBoolean()) {
            attribute_map["FMLAT"] = project.archives_base_name + "_at.cfg"
        }
        attributes(attribute_map)
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}